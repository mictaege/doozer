plugins {
    id "org.sonarqube" version "2.5"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group 'com.github.mictaege'
archivesBaseName = project.name
version '1.5'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'com.github.mictaege:lenientfun:1.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sonarqube {
    properties {
        property "sonar.projectName", "com.github.mictaege.doozer"
        property "sonar.projectKey", "com.github.mictaege.doozer"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

String publishUsername = hasProperty("ossrhUsername") ? property("ossrhUsername") : ""
String publishPassword = hasProperty("ossrhPassword") ? property("ossrhPassword") : ""

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            pom.project {
                name 'doozer'
                packaging 'jar'
                description 'A minimalistic framework that let you create any Java objects with a fluent and readable syntax. Helpful especially in integration and acceptance test scenarios for building complex object trees.'
                url 'https://github.com/mictaege/doozer'
                scm {
                    connection 'https://github.com/mictaege/doozer.git'
                    url 'https://github.com/mictaege/doozer'
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'mictaege'
                        name 'Michael Taege'
                    }
                }
            }
        }
    }
}
